Existence of lines through given points in projective space
===========================================================
Given two distinct points in â„™â¿,
we show that there exists a line in â„™â¿ interpolating between these points,
that is, a function â„™Â¹ â†’ â„™â¿ that hits the points.

```agda
{-# OPTIONS --safe #-}

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Structure
open import Cubical.Foundations.Powerset using (_âˆˆ_)
open import Cubical.Foundations.HLevels using (isPropâ†’)
open import Cubical.Foundations.Function using (case_of_)

import Cubical.HITs.SetQuotients as SQ
import Cubical.HITs.PropositionalTruncation as PT
open import Cubical.Data.Nat as â„• using (â„•)
open import Cubical.Data.FinData
open import Cubical.Data.Sigma
open import Cubical.Data.Empty as âŠ¥ using (âŠ¥; isPropâŠ¥)

open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRing.LocalRing
open import Cubical.Algebra.Ring using (module RingTheory)
open import Cubical.Algebra.Module
open import Cubical.Algebra.Module.Instances.FinVec
open import Cubical.Algebra.AbGroup using (module AbGroupTheory)

open import Cubical.Relation.Nullary.Base using (Â¬_; yes; no)

import SyntheticGeometry.SQC

module SyntheticGeometry.ProjectiveSpace.LineThroughPoints
  {â„“ : Level}
  (k : CommRing â„“)
  (k-local : isLocal k)
  (k-sqc : SyntheticGeometry.SQC.sqc-over-itself k)
  where

open import SyntheticGeometry.ProjectiveSpace k k-local k-sqc
open import SyntheticGeometry.SQC.Consequences k k-local k-sqc
open import SyntheticGeometry.ProjectiveSpace.StandardPoints k k-local k-sqc
open import SyntheticGeometry.ProjectiveSpace.CharacterizationOfLinearEquivalence k k-local k-sqc


private
  [_] : {n : â„•} â†’ ğ”¸â¿âºÂ¹-0 n â†’ â„™ n
  [_] = SQ.[_]
```

We construct a line through two distinct points in projective space,
assuming that fixed representatives for the points are given.

Note:
We could alternatively show that
(1) injective maps of vector spaces induce maps of projective spaces and
(2) two nonzero vectors are linearly independent iff they represent different points in â„™â¿.

```agda
module _
  {n : â„•}
  ((a , aâ‰ 0) (b , bâ‰ 0) : ğ”¸â¿âºÂ¹-0 n)
  ([a]â‰ [b] : Â¬ [ a , aâ‰ 0 ] â‰¡ [ b , bâ‰ 0 ])
  where

  private
    module k = CommRingStr (snd k)
    ğ”¸â¿âºÂ¹-as-module = FinVecLeftModule (CommRingâ†’Ring k) {n = n â„•.+ 1}
    module ğ”¸â¿âºÂ¹ = LeftModuleStr (str ğ”¸â¿âºÂ¹-as-module)
  open k using (_Â·_; -_; 0r; 1r)
  open ğ”¸â¿âºÂ¹ hiding (-_)
```

For the construction of the map â„™Â¹ â†’ â„™â¿,
we first assume a given representative x : ğ”¸Â²-0 for the input point in â„™Â¹.

```agda
  module Construction
    ((x , xâ‰ 0) : ğ”¸â¿âºÂ¹-0 1)
    where

    xâ‚€ = x zero
    xâ‚ = x one
```

Here is the output value we wish to assign to the input x.

```agda
    value : ğ”¸â¿âºÂ¹ n
    value = (xâ‚€ â‹† a) + (xâ‚ â‹† b)
```

We have to show that this intended output value is non-zero.

```agda
    module NonZero
      (valueâ‰¡0 : value â‰¡ 0ğ”¸â¿âºÂ¹ n)
      where

      private
        module k-Th = RingTheory (CommRingâ†’Ring k)

      open Units k
      open AbGroupTheory (LeftModuleâ†’AbGroup ğ”¸â¿âºÂ¹-as-module)
      open ModuleTheory _ ğ”¸â¿âºÂ¹-as-module

      xâ‚€-invâ†’[b]â‰¡[a] : (xâ‚€ âˆˆ k Ë£) â†’ [ b , bâ‰ 0 ] â‰¡ [ a , aâ‰ 0 ]
      xâ‚€-invâ†’[b]â‰¡[a] xâ‚€-inv =
        SQ.eq/ _ _ (char (b , bâ‰ 0) (a , aâ‰ 0) (xâ‚€â»Â¹ Â· (- xâ‚)) (
          ((xâ‚€â»Â¹ Â· (- xâ‚)) â‹† b)      â‰¡âŸ¨ â‹†Assoc _ _ _ âŸ©
          (xâ‚€â»Â¹ â‹† ((- xâ‚) â‹† b))      â‰¡âŸ¨ cong (xâ‚€â»Â¹ â‹†_) (
            ((- xâ‚) â‹† b)           â‰¡âŸ¨ cong (_â‹† b) (k-Th.-IsMult-1 _) âŸ©
            (((- 1r) Â· xâ‚) â‹† b)    â‰¡âŸ¨ â‹†Assoc _ _ _ âŸ©
            ((- 1r) â‹† (xâ‚ â‹† b))    â‰¡âŸ¨ minusByMult _ âŸ©
            (ğ”¸â¿âºÂ¹.- xâ‚ â‹† b)       â‰¡âŸ¨ sym (implicitInverse (+Comm _ _ âˆ™ valueâ‰¡0)) âŸ©
            (xâ‚€ â‹† a)               âˆ ) âŸ©
          (xâ‚€â»Â¹ â‹† (xâ‚€ â‹† a))          â‰¡âŸ¨ sym (â‹†Assoc _ _ _) âŸ©
          ((xâ‚€â»Â¹ Â· xâ‚€) â‹† a)          â‰¡âŸ¨ cong (_â‹† a) (Â·-linv _) âŸ©
          (1r â‹† a)                   â‰¡âŸ¨ â‹†IdL _ âŸ©
          a                          âˆ ))
        where
        instance
          _ = xâ‚€-inv
        xâ‚€â»Â¹ = xâ‚€ â»Â¹

      xâ‚-invâ†’[a]â‰¡[b] : (xâ‚ âˆˆ k Ë£) â†’ [ a , aâ‰ 0 ] â‰¡ [ b , bâ‰ 0 ]
      xâ‚-invâ†’[a]â‰¡[b] xâ‚-inv =
        SQ.eq/ _ _ (char (a , aâ‰ 0) (b , bâ‰ 0) (xâ‚â»Â¹ Â· (- xâ‚€)) (
          ((xâ‚â»Â¹ Â· (- xâ‚€)) â‹† a)      â‰¡âŸ¨ â‹†Assoc _ _ _ âŸ©
          (xâ‚â»Â¹ â‹† ((- xâ‚€) â‹† a))      â‰¡âŸ¨ cong (xâ‚â»Â¹ â‹†_) (
            ((- xâ‚€) â‹† a)           â‰¡âŸ¨ cong (_â‹† a) (k-Th.-IsMult-1 _) âŸ©
            (((- 1r) Â· xâ‚€) â‹† a)    â‰¡âŸ¨ â‹†Assoc _ _ _ âŸ©
            ((- 1r) â‹† (xâ‚€ â‹† a))    â‰¡âŸ¨ minusByMult _ âŸ©
            (ğ”¸â¿âºÂ¹.- xâ‚€ â‹† a)       â‰¡âŸ¨  sym (implicitInverse valueâ‰¡0) âŸ©
            (xâ‚ â‹† b)               âˆ ) âŸ©
          (xâ‚â»Â¹ â‹† (xâ‚ â‹† b))          â‰¡âŸ¨ sym (â‹†Assoc _ _ _) âŸ©
          ((xâ‚â»Â¹ Â· xâ‚) â‹† b)          â‰¡âŸ¨ cong (_â‹† b) (Â·-linv _) âŸ©
          (1r â‹† b)                   â‰¡âŸ¨ â‹†IdL _ âŸ©
          b                          âˆ ))
        where
        instance
          _ = xâ‚-inv
        xâ‚â»Â¹ = xâ‚ â»Â¹

      non-zero : âŠ¥
      non-zero =
        PT.rec
          isPropâŠ¥
          (Î»{ (zero , xâ‚€-inv) â†’ [a]â‰ [b] (sym (xâ‚€-invâ†’[b]â‰¡[a] xâ‚€-inv))
            ; (one , xâ‚-inv) â†’ [a]â‰ [b] (xâ‚-invâ†’[a]â‰¡[b] xâ‚-inv)
            })
          (generalized-field-property x xâ‰ 0)

  open Construction

  private
    respects-linear-equivalence :
      (x : ğ”¸â¿âºÂ¹-0 1) â†’
      (y : ğ”¸â¿âºÂ¹-0 1) â†’
      linear-equivalent 1 (fst x) (fst y) â†’
      linear-equivalent n (value x) (value y)
    respects-linear-equivalence (x , xâ‰ 0) (y , yâ‰ 0) (c , c-inv , cxâ‰¡y) =
      c , c-inv ,
      ( (c â‹† ((x zero â‹† a) + (x one â‹† b)))        â‰¡âŸ¨ â‹†DistR+ _ _ _ âŸ©
        ((c â‹† (x zero â‹† a)) + (c â‹† (x one â‹† b)))  â‰¡âŸ¨ sym (congâ‚‚ _+_ (â‹†Assoc _ _ _) (â‹†Assoc _ _ _)) âŸ©
        (((c Â· x zero) â‹† a) + ((c Â· x one) â‹† b))  â‰¡âŸ¨ congâ‚‚ _+_ (cong (_â‹† a) (funExtâ» cxâ‰¡y zero))
                                                               (cong (_â‹† b) (funExtâ» cxâ‰¡y one)) âŸ©
        ((y zero â‹† a) + (y one â‹† b))              âˆ )

  line-through-points : â„™ 1 â†’ â„™ n
  line-through-points = SQ.rec SQ.squash/
    (Î» x â†’ [ value x , non-zero x ])
    Î» x y rel â†’ SQ.eq/ _ _ (respects-linear-equivalence x y rel)
    where
    open NonZero

  open StandardPoints {n = 1}
  open ModuleTheory _ ğ”¸â¿âºÂ¹-as-module

  line-hits-point-0 : line-through-points (p zero) â‰¡ [ a , aâ‰ 0 ]
  line-hits-point-0 = cong [_] (Î£â‰¡Prop (Î» _ â†’ isPropâ†’ isPropâŠ¥) (
    ((1r â‹† a) + (0r â‹† b))  â‰¡âŸ¨ congâ‚‚ _+_ (â‹†IdL _) (â‹†AnnihilL _) âŸ©
    (a + 0m)               â‰¡âŸ¨ +IdR _ âŸ©
    a                      âˆ))

  line-hits-point-1 : line-through-points (p one) â‰¡ [ b , bâ‰ 0 ]
  line-hits-point-1 = cong [_] (Î£â‰¡Prop (Î» _ â†’ isPropâ†’ isPropâŠ¥) ((
    ((0r â‹† a) + (1r â‹† b))  â‰¡âŸ¨ congâ‚‚ _+_ (â‹†AnnihilL _) (â‹†IdL _) âŸ©
    (0m + b)               â‰¡âŸ¨ +IdL _ âŸ©
    b                      âˆ)))
```

If we are given distinct points (instead of representatives),
we conclude the (mere) existence of a map â„™Â¹ â†’ â„™â¿ that hits these points.
(The image of this map should be uniquely determined by the points,
but not the map itself.)

```agda
module _
  {n : â„•}
  (p q : â„™ n)
  (pâ‰ q : Â¬ p â‰¡ q)
  where

  module Std = StandardPoints {n = 1}

  line-through-points-exists : âˆƒ[ l âˆˆ (â„™ 1 â†’ â„™ n) ] (l (Std.p zero) â‰¡ p) Ã— (l (Std.p one) â‰¡ q)
  line-through-points-exists =
    PT.map2
      (Î» (a , [a]â‰¡p) (b , [b]â‰¡q) â†’
        let
          [a]â‰ [b] : Â¬ [ a ] â‰¡ [ b ]
          [a]â‰ [b] [a]â‰¡[b] = pâ‰ q (
            p      â‰¡âŸ¨ sym [a]â‰¡p âŸ©
            [ a ]  â‰¡âŸ¨ [a]â‰¡[b] âŸ©
            [ b ]  â‰¡âŸ¨ [b]â‰¡q âŸ©
            q      âˆ )
        in
        line-through-points a b [a]â‰ [b] ,
        line-hits-point-0 a b [a]â‰ [b] âˆ™ [a]â‰¡p ,
        line-hits-point-1 a b [a]â‰ [b] âˆ™ [b]â‰¡q)
      (SQ.[]surjective p)
      (SQ.[]surjective q)
```
