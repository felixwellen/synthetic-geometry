Facts about the projective line â„™Â¹
==================================

```agda
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Structure
open import Cubical.Foundations.Powerset using (_âˆˆ_; _âŠ†_) renaming (â„™ to Powerset)
open import Cubical.Foundations.Function
open import Cubical.Foundations.HLevels

open import Cubical.Functions.Embedding
open import Cubical.Functions.Surjection
open import Cubical.Functions.Involution

open import Cubical.HITs.SetQuotients as SQ
open import Cubical.HITs.PropositionalTruncation as PT using (âˆ¥_âˆ¥â‚)
open import Cubical.HITs.Pushout as Pushout

open import Cubical.Data.FinData
open import Cubical.Data.Sigma

open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRing.LocalRing

import SyntheticGeometry.SQC

module SyntheticGeometry.ProjectiveLine
  {â„“ : Level}
  (k : CommRing â„“)
  (k-local : isLocal k)
  (k-sqc : SyntheticGeometry.SQC.sqc-over-itself k)
  where

open import SyntheticGeometry.Spec k
open import SyntheticGeometry.Open k
open import SyntheticGeometry.Affine k k-local k-sqc
open import SyntheticGeometry.ProjectiveSpace k k-local k-sqc
open SyntheticGeometry.SQC k
```

Exhibit â„™Â¹ as a pushout of two copies of ğ”¸Â¹.

```agda
ğ”¸Â¹Ë£ : Type â„“
ğ”¸Â¹Ë£ = Î£[ x âˆˆ âŸ¨ k âŸ© ] x âˆˆ k Ë£

module PushoutMaps
  where
  f g : ğ”¸Â¹Ë£ â†’ âŸ¨ k âŸ©
  f (x , _) = x
  g (_ , (xâ»Â¹ , _)) = xâ»Â¹

open PushoutMaps

â„™Â¹-as-pushout : Type â„“
â„™Â¹-as-pushout =
  Pushout {A = ğ”¸Â¹Ë£} {B = âŸ¨ k âŸ©} {C = âŸ¨ k âŸ©} f g

module Comparison
  where

  open CommRingStr (snd k)
  open Consequences k k-local
  open Units k

  ğ”¸Â² : Type â„“
  ğ”¸Â² = ğ”¸â¿âºÂ¹ 1

  ğ”¸Â²-0 : Type â„“
  ğ”¸Â²-0 = ğ”¸â¿âºÂ¹-0 1

  ğ”¸Â²-â‰¡ :
    {xy x'y' : ğ”¸Â²} â†’
    (xy zero â‰¡ x'y' zero) â†’
    (xy one â‰¡ x'y' one) â†’
    xy â‰¡ x'y'
  ğ”¸Â²-â‰¡ xâ‰¡x' yâ‰¡y' = funExt (Î»{ zero â†’ xâ‰¡x' ; one â†’ yâ‰¡y'})

  -- More specific types for some operations.
  [_]â„™Â¹ : ğ”¸Â²-0 â†’ â„™ 1
  [_]â„™Â¹ = [_]

  inl' inr' : âŸ¨ k âŸ© â†’ â„™Â¹-as-pushout
  inl' = inl
  inr' = inr

  -- The autoequivalence of ğ”¸Â¹Ë£ that turns f into g and vice versa.
  inversion : ğ”¸Â¹Ë£ â†’ ğ”¸Â¹Ë£
  inversion (x , x-inv) = (x â»Â¹) , RË£InvClosed x
    where
    instance
      _ : x âˆˆ k Ë£
      _ = x-inv

  -- Just checking that this is definitional.
  gâ‰¡fâˆ˜inversion : (x : ğ”¸Â¹Ë£) â†’ g x â‰¡ f (inversion x)
  gâ‰¡fâˆ˜inversion x = refl

  isEquiv-inversion : isEquiv inversion
  isEquiv-inversion = involIsEquiv (Î» x â†’ Î£â‰¡Prop (snd âˆ˜ (k Ë£)) refl)

  isSet-â„™Â¹-as-pushout : isSet â„™Â¹-as-pushout
  isSet-â„™Â¹-as-pushout =
    Pushout.preserveHLevelEmbedding
      f
      g
      {n = 0}
      isEmbedding-f
      (isEmbedding-âˆ˜ isEmbedding-f (isEquivâ†’isEmbedding isEquiv-inversion))
      is-set
      is-set
    where
    isEmbedding-f = snd (snd (Subsetâ†’Embedding (k Ë£)))

  module Function
    where

    Î¹â‚€ Î¹â‚ : âŸ¨ k âŸ© â†’ ğ”¸Â²-0
    fst (Î¹â‚€ x) = Î»{ zero â†’ 1r ; one â†’ x}
    snd (Î¹â‚€ x) â‰¡0 = 1â‰¢0 (funExtâ» â‰¡0 zero)
    fst (Î¹â‚ x) = Î»{ zero â†’ x ; one â†’ 1r}
    snd (Î¹â‚ x) â‰¡0 = 1â‰¢0 (funExtâ» â‰¡0 one)

    path : (x y : âŸ¨ k âŸ©) â†’ x Â· y â‰¡ 1r â†’ [ Î¹â‚€ x ]â„™Â¹ â‰¡ [ Î¹â‚ y ]â„™Â¹
    -- The converse to this appears in Injectivity.intersection-case below.
    path x y xyâ‰¡1 =
      let yxâ‰¡1 : y Â· x â‰¡ 1r
          yxâ‰¡1 = Â·Comm y x âˆ™ xyâ‰¡1
      in eq/ _ _ ( (y , ((x , yxâ‰¡1) , funExt (Î»{ zero â†’ Â·IdR y ; one â†’ yxâ‰¡1 }) )) )

    Ï• : â„™Â¹-as-pushout â†’ â„™ 1
    Ï• (inl x) = [ Î¹â‚€ x ]â„™Â¹
    Ï• (inr x) = [ Î¹â‚ x ]â„™Â¹
    Ï• (push (x , y , xyâ‰¡1) i) = path x y xyâ‰¡1 i

  open Function

  module Surjectivity
    where

    isSurjection-Ï• : isSurjection Ï•
    isSurjection-Ï• =
      SQ.elimProp
        (Î» _ â†’ PT.isPropPropTrunc)
        Î»{ (xy , xyâ‰¢0) â†’
          PT.map
            (uncurry (inner (xy , xyâ‰¢0)))
            (generalized-field-property k-local k-sqc xy xyâ‰¢0)
        }
      where
      computation :
        (x y : âŸ¨ k âŸ©) â†’
        {{x-inv : x âˆˆ k Ë£}} â†’
        let instance _ = x-inv in
        (x Â· (x â»Â¹ Â· y)) â‰¡ y
      computation x y =
        (x Â· (x â»Â¹ Â· y)  â‰¡âŸ¨ Â·Assoc _ _ _ âŸ©
        x Â· x â»Â¹ Â· y    â‰¡âŸ¨ cong (_Â· _) (Â·-rinv x) âŸ©
        1r Â· y          â‰¡âŸ¨ Â·IdL y âŸ©
        y               âˆ)
      module _
        ((xy , xyâ‰¢0) : ğ”¸Â²-0)
        where
        x = xy zero
        y = xy one

        inner : (i : Fin 2) â†’ (xy i âˆˆ (k Ë£)) â†’ fiber Ï• [ xy , xyâ‰¢0 ]
        inner zero x-inv =
          let instance _ = x-inv in
            inl (x â»Â¹ Â· y)
          , eq/ (Î¹â‚€ (x â»Â¹ Â· y))
                (xy , xyâ‰¢0)
                (x , x-inv , ğ”¸Â²-â‰¡ (Â·IdR x) (computation x y))
        inner one y-inv =
          let instance _ = y-inv in
            inr (y â»Â¹ Â· x)
          , eq/ (Î¹â‚ (y â»Â¹ Â· x))
                (xy , xyâ‰¢0)
                (y , y-inv , ğ”¸Â²-â‰¡ (computation y x) (Â·IdR y))

  module Injectivity
    where

    isProp-â‰¡â†’â‰¡ : {q q' : â„™ 1} â†’ {p p' : â„™Â¹-as-pushout} â†’ isProp (q â‰¡ q' â†’ p â‰¡ p')
    isProp-â‰¡â†’â‰¡ = isPropâ†’ (isSet-â„™Â¹-as-pushout _ _)

    intersection-case :
      (x x' : âŸ¨ k âŸ©) â†’
      [ Î¹â‚€ x ]â„™Â¹ â‰¡ [ Î¹â‚ x' ]â„™Â¹ â†’
      inl' x â‰¡ inr' x'
    intersection-case x x' e =
      PT.rec
      (isSet-â„™Â¹-as-pushout _ _)
      (Î»{ (s , s-inv , s1xâ‰¡x'1) â†’
            push (x , x' , (x Â· x'        â‰¡âŸ¨ Â·Comm _ _ âŸ©
                            x' Â· x        â‰¡âŸ¨ cong (_Â· x) (sym (funExtâ» s1xâ‰¡x'1 zero)) âŸ©
                            (s Â· 1r) Â· x  â‰¡âŸ¨ cong (_Â· x) (Â·IdR s) âŸ©
                            s Â· x         â‰¡âŸ¨ funExtâ» s1xâ‰¡x'1 one âŸ©
                            1r            âˆ))
        })
      (â„™â¿-effective-quotient 1 e)

    is-injective-Ï• : (p p' : â„™Â¹-as-pushout) â†’ Ï• p â‰¡ Ï• p' â†’ p â‰¡ p'
    is-injective-Ï• =
      Pushout.elimProp
        (Î» p â†’ (p' : _) â†’ Ï• p â‰¡ Ï• p' â†’ p â‰¡ p')
        (Î» _ â†’ isPropÎ  (Î» _ â†’ isProp-â‰¡â†’â‰¡))
        (Î» x â†’ Pushout.elimProp
          (Î» p' â†’ Ï• (inl x) â‰¡ Ï• p' â†’ inl x â‰¡ p')
          (Î» _ â†’ isProp-â‰¡â†’â‰¡)
          (Î» x' e â†’ PT.rec
            (isSet-â„™Â¹-as-pushout _ _)
            (Î»{ (s , s-inv , s1xâ‰¡1x') â†’
              cong inl' (x             â‰¡âŸ¨ sym (Â·IdL x) âŸ©
                        1r Â· x         â‰¡âŸ¨ cong (_Â· x) (sym (funExtâ» s1xâ‰¡1x' zero))  âŸ©
                        (s Â· 1r) Â· x   â‰¡âŸ¨ cong (_Â· x) (Â·IdR s) âŸ©
                        s Â· x          â‰¡âŸ¨ funExtâ» s1xâ‰¡1x' one âŸ©
                        x'             âˆ)
            })
            (â„™â¿-effective-quotient 1 e))
          (Î» x' â†’ intersection-case x x')
        )
        (Î» x â†’ Pushout.elimProp
          (Î» p' â†’ Ï• (inr x) â‰¡ Ï• p' â†’ inr x â‰¡ p')
          (Î» _ â†’ isProp-â‰¡â†’â‰¡)
          (Î» x' â†’ sym âˆ˜ intersection-case x' x âˆ˜ sym)
          (Î» x' e â†’ PT.rec
            (isSet-â„™Â¹-as-pushout _ _)
            (Î»{ (s , s-inv , sx1â‰¡x'1) â†’
              cong inr' (x             â‰¡âŸ¨ sym (Â·IdL x) âŸ©
                        1r Â· x         â‰¡âŸ¨ cong (_Â· x) (sym (funExtâ» sx1â‰¡x'1 one))  âŸ©
                        (s Â· 1r) Â· x   â‰¡âŸ¨ cong (_Â· x) (Â·IdR s) âŸ©
                        s Â· x          â‰¡âŸ¨ funExtâ» sx1â‰¡x'1 zero âŸ©
                        x'             âˆ)
            })
            (â„™â¿-effective-quotient 1 e))
        )

  isEquiv-Ï• : isEquiv Ï•
  isEquiv-Ï• =
    isEmbeddingÃ—isSurjectionâ†’isEquiv
      ( injEmbedding squash/ (Î» {p} {p'} â†’ is-injective-Ï• p p')
      , isSurjection-Ï• )
    where
    open Surjectivity
    open Injectivity

comparison-equiv : â„™Â¹-as-pushout â‰ƒ â„™ 1
fst comparison-equiv = Comparison.Function.Ï•
snd comparison-equiv = Comparison.isEquiv-Ï•
```
